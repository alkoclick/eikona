buildscript {
    ext.kotlin_version = "1.7.10"
    ext.jackson_version = "2.14.2"
    ext.ktor_version = '2.1.1'
    ext.exposed_version = '0.41.1'
    ext.prometheus_version = '0.16.0'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'io.miret.etienne.sass' version '1.4.2'
    id 'com.google.cloud.tools.jib' version '3.3.1'
}
apply plugin: 'kotlin'
apply plugin: 'application'

group = 'io.eikona'
mainClassName = 'Entrypoint'

compileSass {
    outputDir = project.file("${buildDir}/resources/main/assets/css")
}

jar {
    dependsOn compileSass
    from compileSass.outputDir
}

jib {
    to {
        image = 'ghcr.io/alkoclick/eikona'
        auth {
            username = System.getenv('GITHUB_ACTOR') ?: 'alkoclick'
            password = System.getenv('REGISTRY_TOKEN')
        }
    }
    container {
        labels = ['org.opencontainers.image.source': 'https://github.com/alkoclick/eikona']
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    dependsOn compileSass
    kotlinOptions.jvmTarget = "11"
}

repositories {
    mavenCentral()
}


dependencies {
    // Arrow
    implementation(platform("io.arrow-kt:arrow-stack:1.1.2"))
    implementation("io.arrow-kt:arrow-core")

    // Config
    implementation 'com.sksamuel.hoplite:hoplite-core:2.7.3'
    implementation 'com.sksamuel.hoplite:hoplite-hocon:2.7.3'

    // JSON
    // Jackson
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"

    // Various utils
    implementation 'ch.qos.logback:logback-classic:1.4.4'
    implementation "com.squareup.okhttp3:okhttp:4.10.0"

    // Monitoring
    implementation "io.prometheus:simpleclient:$prometheus_version"
    implementation "io.prometheus:simpleclient_hotspot:$prometheus_version"
    implementation "io.prometheus:simpleclient_logback:$prometheus_version"

    // Persistence stuff
    implementation 'org.mapdb:mapdb:3.0.8'
    implementation 'org.xerial:sqlite-jdbc:3.39.3.0'
    implementation "org.jetbrains.exposed:exposed-core:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"
    implementation "org.xerial:sqlite-jdbc:3.30.1"

    // Ktor HTTP server
    implementation "io.ktor:ktor-server-auth:$ktor_version"
    implementation "io.ktor:ktor-server-auth-jwt:$ktor_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-html-builder:$ktor_version"
    implementation "io.ktor:ktor-server-metrics-micrometer:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-server-sessions:$ktor_version"
    testImplementation("io.ktor:ktor-server-test-host:$ktor_version") {
        /// Damn Junit 4 always being pulled in randomly
        exclude group: 'junit', module: 'junit'
    }
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

test {
    useJUnitPlatform()

    afterEvaluate {
        testLogging {
            events 'passed', 'failed', 'skipped', 'PASSED', 'FAILED', 'SKIPPED'
        }
    }
}
